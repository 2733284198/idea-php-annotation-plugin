import org.gradle.api.tasks.testing.logging.TestExceptionFormat

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url 'https://dl.bintray.com/jetbrains/intellij-plugin-service'
        }

    }
    dependencies {
        classpath "org.jetbrains.intellij.plugins:gradle-intellij-plugin:0.5.0-SNAPSHOT"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.4.8"
    id 'com.palantir.git-version' version "0.11.0"
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
sourceCompatibility = 1.8
targetCompatibility = 1.8

def requiredPlugins = [
    "com.jetbrains.php:${phpPluginVersion}",
    "de.espend.idea.php.toolbox:${toolboxPluginVersion}",
    "com.jetbrains.twig:${twigPluginVersion}",
    'CSS',
    'java',
    'java-i18n',
    'properties'
]

if(ideaVersion.startsWith("IU-2019.1")) {
    requiredPlugins.removeAll { (it.toLowerCase() == 'java') }
}

intellij {
    version ideaVersion
    updateSinceUntilBuild false
    plugins = requiredPlugins
    pluginName 'PHP Annotations'


    // Can't instantiate configurable for PHP Toolbox
    // at de.espend.idea.php.toolbox.ui.application.ToolboxApplicationForm.<init>(ToolboxApplicationForm.java:26)
    tasks {
        "buildSearchableOptions" {
            enabled = false
        }
    }
}

patchPluginXml {
    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
    sinceBuild '191'
}

publishPlugin {
    token System.getenv('IJ_TOKEN')
}

group 'de.espend.idea.php.annotation'

def details = versionDetails()
if (details.isCleanTag) {
    version = "${details.lastTag}"
} else {
    version = "${details.lastTag}.${details.gitHash}-SNAPSHOT"
}

wrapper {
    gradleVersion '5.4.1'
}

test.testLogging.exceptionFormat = TestExceptionFormat.FULL
